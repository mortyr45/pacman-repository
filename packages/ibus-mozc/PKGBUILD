# Maintainer: Nocifer <apmichalopoulos at gmail dot com>
# Contributor: UTUMI Hirosi <utuhiro78 at yahoo dot co dot jp>
# Contributor: Felix Yan <felixonmars@gmail.com>
# Contributor: ponsfoot <cabezon dot hashimoto at gmail dot com>

pkgname='ibus-mozc'
pkgver=2.29.5374.102
pkgrel=1
pkgdesc='Mozc module for IBus'
arch=('x86_64')
url='https://github.com/google/mozc'
license=('Apache-2.0 AND BSD-2-Clause AND BSD-3-Clause AND LGPL-3.0-only AND MIT AND NAIST-2003 AND Unicode-3.0')
depends=('ibus>=1.4.1' 'mozc>=2.29.5374.102')
makedepends=('bazel' 'git' 'python' 'qt6-base')
options=(!distcc !ccache)
source=("${pkgname}-git::git+https://github.com/google/mozc.git#commit=c2fcbf6515c5884437977de46187c16a8cb7bb50")
sha256sums=('SKIP')

prepare() {
    cd ${pkgname}-git/src

    git submodule update --init --recursive
}

build() {
    cd ${pkgname}-git/src

    unset ANDROID_NDK_HOME
    unset ANDROID_HOME
    export JAVA_HOME='/usr/lib/jvm/java-11-openjdk/'
    bazel build renderer/qt:mozc_renderer unix/ibus:ibus_mozc unix/icons --config oss_linux --compilation_mode opt
}

package() {
    cd ${pkgname}-git/src

    install -Dm644 ../LICENSE                                   "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
    install -Dm644 data/installer/credits_en.html               "${pkgdir}"/usr/share/licenses/${pkgname}/Submodules

    install -Dm755 bazel-bin/renderer/qt/mozc_renderer          "${pkgdir}"/usr/lib/mozc/mozc_renderer

    install -Dm755 bazel-bin/unix/ibus/ibus_mozc                "${pkgdir}"/usr/lib/${pkgname}/ibus-engine-mozc
    install -Dm644 bazel-bin/unix/ibus/mozc.xml                 "${pkgdir}"/usr/share/ibus/component/mozc.xml

    cd bazel-bin/unix

    unzip -o icons.zip

    install -Dm644 mozc.png                                     "${pkgdir}"/usr/share/${pkgname}/product_icon.png
    install -Dm644 alpha_full.svg                               "${pkgdir}"/usr/share/${pkgname}/alpha_full.svg
    install -Dm644 alpha_half.svg                               "${pkgdir}"/usr/share/${pkgname}/alpha_half.svg
    install -Dm644 direct.svg                                   "${pkgdir}"/usr/share/${pkgname}/direct.svg
    install -Dm644 hiragana.svg                                 "${pkgdir}"/usr/share/${pkgname}/hiragana.svg
    install -Dm644 katakana_full.svg                            "${pkgdir}"/usr/share/${pkgname}/katakana_full.svg
    install -Dm644 katakana_half.svg                            "${pkgdir}"/usr/share/${pkgname}/katakana_half.svg
    install -Dm644 outlined/dictionary.svg                      "${pkgdir}"/usr/share/${pkgname}/dictionary.svg
    install -Dm644 outlined/properties.svg                      "${pkgdir}"/usr/share/${pkgname}/properties.svg
    install -Dm644 outlined/tool.svg                            "${pkgdir}"/usr/share/${pkgname}/tool.svg
}
